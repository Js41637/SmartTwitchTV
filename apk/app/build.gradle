apply from: '../versions.gradle'
apply plugin: 'com.android.application'

//Crashlytics
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
//Crashlytics end

android {
    namespace "com.fgl27.twitch"
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.fgl27.twitch"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.publishVersionCode
        versionName versions.publishVersion + versions.publishVersionCode
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles = [
                    getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            ]
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
    lint {
        abortOnError false
        disable 'SetJavaScriptEnabled', 'GoogleAppIndexingWarning', 'IconMissingDensityFolder', 'IconLauncherShape', 'RtlHardcoded', 'ObsoleteLintCustomCheck'
        lintConfig file('lint.xml')
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //Exoplayer
    implementation project(':media-lib-exoplayer')
    implementation project(':media-lib-ui')
    implementation project(':media-lib-exoplayer-hls')

    //Crashlytics
    implementation platform('com.google.firebase:firebase-bom:' + versions.firebase_bom)
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    //Crashlytics end

    implementation 'androidx.leanback:leanback:' + versions.leanback
    implementation 'androidx.webkit:webkit:' + versions.webkit
    implementation 'com.google.code.gson:gson:' + versions.googlegson
    implementation 'net.grandcentrix.tray:tray:' + versions.tray
    implementation 'androidx.tvprovider:tvprovider:' + versions.tvprovider
    implementation 'androidx.media3:media3-exoplayer:1.3.0'

}
